/*
Copyright (C) 2020 Craig Schneiderwent.  All rights reserved.  I accept
no liability for damages of any kind resulting from the use of this 
software.  Use at your own risk.

This software may be modified and distributed under the terms
of the MIT license. See the LICENSE file for details.

This is intended to be an ANTLR4 grammar for the EXEC DLI interface.

*/

lexer grammar DLILexer;

@lexer::members {
	int parenNesting = 0;
	
	/*
	These Booleans are intended to be set by an invoking application.  It
	is syntactically correct for portions of a CICS command to be commented
	out in a way that is syntactically correct for the host language, so I
	am presuming it is also correct for the EXEC DLI interface.
	*/
	public static Boolean pliCode = false;
	public static Boolean classicCOBOLCode = false;
	public static Boolean freeFormCOBOLCode = false;
	public static Boolean cCode = false;
}

fragment A:'A';
fragment B:'B';
fragment C:'C';
fragment D:'D';
fragment E:'E';
fragment F:'F';
fragment G:'G';
fragment H:'H';
fragment I:'I';
fragment J:'J';
fragment K:'K';
fragment L:'L';
fragment M:'M';
fragment N:'N';
fragment O:'O';
fragment P:'P';
fragment Q:'Q';
fragment R:'R';
fragment S:'S';
fragment T:'T';
fragment U:'U';
fragment V:'V';
fragment W:'W';
fragment X:'X';
fragment Y:'Y';
fragment Z:'Z';

fragment SPLAT: '*';
fragment SLASH: '/';
fragment GT: '>';

CLASSIC_COBOL_LINE_NUMBER
	: (~[\n\r] ~[\n\r] ~[\n\r] ~[\n\r] ~[\n\r] ~[\n\r])
	{!freeFormCOBOLCode && classicCOBOLCode && getCharPositionInLine() == 6}?
	->channel(HIDDEN)
	;

fragment CLASSIC_COBOL_COMMENT_FLAG
	: (~[\n\r] ~[\n\r] ~[\n\r] ~[\n\r] ~[\n\r] ~[\n\r] SPLAT)
	{!freeFormCOBOLCode && classicCOBOLCode && getCharPositionInLine() == 7}?
	;

CLASSIC_COBOL_COMMENT
	: (CLASSIC_COBOL_COMMENT_FLAG .*? [\n\r])
	->channel(HIDDEN)
	;

FREE_FORM_COBOL_COMMENT
	: (SPLAT GT ~[\n\r]* [\n\r])
	{(classicCOBOLCode | freeFormCOBOLCode)}?
	->channel(HIDDEN)
	;

PLI_COMMENT
	: (SLASH SPLAT .*? SPLAT SLASH)
	{pliCode}?
	->channel(HIDDEN)
	;

C_COMMENT
	: (
	(SLASH SPLAT .*? SPLAT SLASH)
	| (SLASH SLASH ~[\n\r]* [\n\r])
	)
	{cCode}?
	->channel(HIDDEN)
	;

EXEC_DLI
	: E X E C (U T E)? [ ]+ D L I
	;

END_EXEC
	: E N D HYPHEN E X E C DOT?
	;

HYPHEN
	: '-'
	;

DOT
	: '.'
	;

SEMICOLON
	: ';'
	;

/*
The nongreedy suffix is necessary to make the CLASSIC_COBOL_COMMENT_FLAG 
work correctly.
*/
WS
	: [ \n\r\t]+?
	->skip
	;

/*
This token indicates an argument to a token follows.
*/

LPAREN
	: '('
	->pushMode(ARG_MODE)
	;


